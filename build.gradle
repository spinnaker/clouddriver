/*
 * Copyright 2014 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

ext {
  springBootVersion = "1.2.5.RELEASE"
}

buildscript {
  ext {
    springBootVersion = "1.2.5.RELEASE"
  }
  repositories {
    jcenter()
    maven { url "http://dl.bintray.com/spinnaker/gradle" }
  }
  dependencies {
    classpath 'com.netflix.spinnaker:spinnaker-gradle-project:1.12.115'
    classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
  }
}

allprojects {
  group = "com.netflix.spinnaker.clouddriver"
  apply plugin: 'spinnaker-gradle-project'
  apply plugin: 'idea'
}

subprojects { project ->
  apply plugin: 'groovy'

  targetCompatibility = 1.8
  sourceCompatibility = 1.8


  repositories {
    maven { url 'http://oss.jfrog.org/repo' }
    jcenter()
    maven { url 'http://dl.bintray.com/spinnaker/spinnaker' }
    maven { url 'http://repo.spring.io/repo' }
  }

  configurations {
    all {
      exclude group: 'javax.servlet', module: 'servlet-api'
      resolutionStrategy {
        force 'org.antlr:antlr-runtime:3.5.2'
        force 'com.amazonaws:aws-java-sdk-core:1.10.5.1'
        eachDependency {
          if (it.requested.group == 'asm' || it.requested.group == 'org.ow2.asm') {
            it.useTarget group: 'org.ow2.asm', name: 'asm-all', version: '5.0.3'
          }
          if (it.requested.group == 'junit') {
            it.useTarget group: 'junit', name: 'junit', version: '4.12'
          }
          if (it.requested.group == 'cglib' || it.requested.name == 'cglib') {
            it.useTarget group: 'cglib', name: 'cglib', version: '3.1'
          }
          if (it.requested.group == 'com.google.guava') {
            it.useTarget group: 'com.google.guava', name: 'guava', version: '18.0'
          }
          if (it.requested.group == 'antlr') {
            it.useTarget group: 'org.antlr', name: it.requested.name, version: '3.5.2'
          }
          if (it.requested.group == 'org.apache.xbean') {
            it.useVersion '4.3'
          }
        }
      }
    }
  }

  dependencies {
    compile spinnaker.dependency('groovy')
    compile 'org.springframework.cloud:spring-cloud-starter-config:1.0.3.RELEASE'
    spinnaker.group('test')
  }
}

tasks.checkSnapshotDependencies.enabled = false
defaultTasks ':clouddriver-web:bootRun'
