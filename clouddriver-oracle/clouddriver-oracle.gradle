
class DownloadTask extends DefaultTask {
  @Input
  String sourceUrl

  @OutputFile
  File target

  @TaskAction
  void download() {
    ant.get(src: sourceUrl, dest: target)
  }
}

final File sdkDownloadLocation = project.file('build/sdkdownload')
final File sdkLocation = project.file('build/oci-java-sdk')

// Oracle BMCS SDK isn't published to any maven repo (yet!), so we manually download, unpack and add to compile/runtime deps
// https://github.com/oracle/oci-java-sdk/issues/25
task fetchSdk(type: DownloadTask) {
  sourceUrl = 'https://github.com/oracle/oci-java-sdk/releases/download/v1.3.2/oci-java-sdk.zip'
  target = sdkDownloadLocation
}

task unpackSdk(type: Sync) {
  dependsOn('fetchSdk')
  from zipTree(tasks.fetchSdk.target)
  into sdkLocation
  include "**/*.jar"
  exclude "**/*-sources.jar"
  exclude "**/*-javadoc.jar"
  exclude "apidocs/**"
  exclude "examples/**"

  // Scary but works. I think clouddriver deps in general need cleaning at some point
  // Even without the oracle bmc sdk 3rd party deps there's still multiple javax.inject and commons-X JARs
  exclude "**/*jackson*.jar"
  exclude "**/*jersey*.jar"
  exclude "**/hk2*.jar"
  exclude "**/*guava*.jar"
  exclude "**/commons*.jar"
  exclude "**/aopalliance*.jar"
  exclude "**/javassist*.jar"
  exclude "**/slf*.jar"
  exclude "**/osgi*.jar"
  exclude "**/validation*.jar"
  exclude "**/jsr305*.jar"
  exclude "**/json-smart*.jar"
  exclude "**/oci-java-sdk-full-shaded-*.jar"
}

task cleanSdk(type: Delete) {
  delete sdkLocation, sdkDownloadLocation
}

tasks.clean.dependsOn('cleanSdk')
tasks.compileJava.dependsOn('unpackSdk')

dependencies {
  implementation project(":clouddriver-core")
  implementation project(":cats:cats-core")
  implementation project(":clouddriver-security")

  implementation fileTree(sdkLocation)

  compileOnly "org.projectlombok:lombok"
  annotationProcessor "org.projectlombok:lombok"
  testAnnotationProcessor "org.projectlombok:lombok"

  implementation "com.google.guava:guava:27.1-jre"
  implementation "com.netflix.frigga:frigga"
  implementation "com.netflix.spectator:spectator-api"
  implementation "com.netflix.spinnaker.fiat:fiat-api:$fiatVersion"
  implementation "com.netflix.spinnaker.fiat:fiat-core:$fiatVersion"
  implementation "com.netflix.spinnaker.moniker:moniker"
  implementation "org.codehaus.groovy:groovy-all"
  implementation "org.springframework.boot:spring-boot-starter-actuator"
  implementation "org.springframework.boot:spring-boot-starter-web"

  testImplementation "cglib:cglib-nodep"
  testImplementation "org.objenesis:objenesis"
  testImplementation "org.spockframework:spock-core"
  testImplementation "org.spockframework:spock-spring"
  testImplementation "org.springframework:spring-test"
}

def allSourceSets = sourceSets

def licenseExtension = project.extensions.findByName('license')
if (licenseExtension != null) {
  licenseExtension.header project.file('oracle-source-header')
  licenseExtension.includes(["**/*.groovy", "**/*.java", "**/*.properties"])
  licenseExtension.strictCheck true
  licenseExtension.skipExistingHeaders false
  licenseExtension.sourceSets = allSourceSets
}
