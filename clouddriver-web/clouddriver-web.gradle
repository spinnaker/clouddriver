apply plugin: 'spring-boot'
apply plugin: 'nebula.ospackage'
apply plugin: 'nebula.ospackage-daemon'

ext {
  springConfigLocation = System.getProperty('spring.config.location', "${System.getProperty('user.home')}/.spinnaker/")
}

tasks.withType(org.springframework.boot.gradle.run.BootRunTask) {
  systemProperty('spring.config.location', project.springConfigLocation)
}

configurations.all {
  exclude group: 'javax.servlet', module: 'servlet-api'
  exclude group: "org.slf4j", module: "slf4j-log4j12"
}

dependencies {
  compile project(':kato:kato-web')
  compile project(':oort:oort-web')
  compile project(':mort:mort-web')

  compile project(':clouddriver-azure')
  compile project(':clouddriver-cf')
  compile project(':clouddriver-google')
  compile project(':clouddriver-kubernetes')

  compile 'javax.ws.rs:jsr311-api:1.1.1'
  runtime spinnaker.dependency('kork')
  compile spinnaker.dependency('korkWeb')
  compile spinnaker.dependency('bootActuator')
  compile spinnaker.dependency('bootDataRest')
  testCompile ("org.springframework.boot:spring-boot-starter-test")
  //this brings in the jetty GzipFilter which boot will autoconfigure
  runtime 'org.eclipse.jetty:jetty-servlets:9.2.11.v20150529'
}

tasks.bootRepackage.enabled = false

applicationName = 'clouddriver'
applicationDefaultJvmArgs = ["-Djava.security.egd=file:/dev/./urandom"]
applicationDistribution.from(project.file('config')) {
  into 'config'
}

sourceSets {
  main {
    resources {
      srcDir 'src/main/resources'
      srcDir 'config'
    }
  }
}

jar {
  doFirst {
    exclude "${rootProject.name}.yml"
  }
}

startScripts {
  defaultJvmOpts = applicationDefaultJvmArgs + ["-Dspring.config.location=/opt/spinnaker/config/"]
  doLast {
    unixScript.text = unixScript.text.replace('DEFAULT_JVM_OPTS=', '''\
        if [ -f /etc/default/spinnaker ]; then
          set -a
          . /etc/default/spinnaker
          set +a
        fi
        DEFAULT_JVM_OPTS='''.stripIndent())
    unixScript.text = unixScript.text.replace('CLASSPATH=$APP_HOME', 'CLASSPATH=$APP_HOME/config:$APP_HOME')
    windowsScript.text = windowsScript.text.replace('set CLASSPATH=', 'set CLASSPATH=%APP_HOME%\\config;')
  }
}

String toVers(String v) {
  int idx = v.indexOf('-')
  if (idx != -1) {
    return v.substring(0, idx)
  }
  return v
}

buildscript {
  repositories { jcenter() }
  dependencies {
    classpath 'com.netflix.nebula:nebula-ospackage-plugin:3.0.0'
  }
}

ospackage {
  requires('daemontools')
  packageName = "spinnaker-" + project.applicationName
  version = toVers(project.version.toString())
  release '4'
  into "/opt/${project.applicationName}"
  from ("${project.buildDir}/install/${project.applicationName}")

  from(file('etc/logrotate.d/clouddriver')) {
    into('/etc/logrotate.d')
    user = 'root'
    permissionGroup = 'root'
    fileType = CONFIG | NOREPLACE
  }
}

daemon {
  daemonName = "clouddriver"
  command = "/opt/clouddriver/bin/clouddriver 2>&1 > /var/log/spinnaker/clouddriver/clouddriver.log &"
  user = 'root'
  runLevels = [2,3,4,5]
}

buildDeb {
  dependsOn installDist
}

buildRpm {
  dependsOn installDist
  os LINUX
}
