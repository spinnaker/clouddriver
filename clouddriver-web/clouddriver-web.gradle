apply plugin: 'spinnaker.application'

if (gradle.includedCloudProviderProjects.contains(':clouddriver-oracle')) {
  tasks.startScripts.dependsOn(':clouddriver-oracle:unpackSdk')
  tasks.startScripts.mustRunAfter(':clouddriver-oracle:unpackSdk')
}

ext {
  springConfigLocation = System.getProperty('spring.config.location', "${System.getProperty('user.home')}/.spinnaker/")
}

mainClassName = 'com.netflix.spinnaker.clouddriver.Main'
run {
  systemProperty('spring.config.location', project.springConfigLocation)
}

configurations.all {
  exclude group: 'javax.servlet', module: 'servlet-api'
  exclude group: "org.slf4j", module: "slf4j-log4j12"
}

repositories {
  maven { url "https://adxsnapshots.azurewebsites.net" }
}

dependencies {
  implementation project(":cats:cats-core")
  implementation project(":clouddriver-artifacts")
  implementation project(":clouddriver-core")
  implementation project(":clouddriver-security")
  implementation project(":clouddriver-sql-mysql")

  
  compileOnly "org.projectlombok:lombok"
  annotationProcessor "org.projectlombok:lombok"
  testAnnotationProcessor "org.projectlombok:lombok"

  implementation "com.netflix.frigga:frigga"
  implementation "com.netflix.spinnaker.fiat:fiat-api:$fiatVersion"
  implementation "com.netflix.spinnaker.fiat:fiat-core:$fiatVersion"
  implementation "com.netflix.spinnaker.kork:kork-artifacts"
  implementation "com.netflix.spinnaker.kork:kork-core"
  implementation "com.netflix.spinnaker.kork:kork-secrets-aws"
  implementation "com.netflix.spinnaker.kork:kork-stackdriver"
  implementation "com.netflix.spinnaker.kork:kork-swagger"
  implementation "com.netflix.spinnaker.kork:kork-web"
  implementation "com.netflix.spinnaker.moniker:moniker"
  implementation "org.codehaus.groovy:groovy-all"
  implementation "org.slf4j:slf4j-api"
  implementation "org.springframework.boot:spring-boot-actuator"
  implementation "org.springframework.boot:spring-boot-starter-data-rest"
  implementation "org.springframework.boot:spring-boot-starter-test"
  implementation "org.springframework.boot:spring-boot-starter-web"
  
  //this brings in the jetty GzipFilter which boot will autoconfigure
  runtime 'org.eclipse.jetty:jetty-servlets:9.2.11.v20150529'

  testImplementation project(":clouddriver-elasticsearch")

  testImplementation "org.spockframework:spock-core"

  // Add each included cloud provider project as a runtime dependency
  gradle.includedCloudProviderProjects.each {
    implementation project(":${it}")
  }
}

